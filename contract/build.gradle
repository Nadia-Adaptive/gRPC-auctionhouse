plugins {
    id 'com.wearadaptive.grpcchat.java-common-conventions'
    id 'com.google.protobuf' version '0.9.2'
    id 'idea'
}

def protocVersion = '3.21.12'
def grpcVersion = libs.versions.grpc.get()
def reactiveGrpcVersion = '1.2.0'

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
        reactor {
            artifact = "com.salesforce.servicelibs:reactor-grpc:${reactiveGrpcVersion}"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
            reactor {}
        }
    }
}

dependencies {
    implementation("com.salesforce.servicelibs:reactor-grpc-stub:${reactiveGrpcVersion}")
    implementation 'io.projectreactor:reactor-core:3.6.3'
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        proto {
            srcDir 'contract/src/main/proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}